package compilador;

import java_cup.runtime.*;
import ast.*;

action code {:
	private boolean modoDepuracion=true;
	private NodoBase ast=null;
	private int lineanum=0;
	
	//Debe ser llamada solo despues de un parse(), en caso contrario retornara null siempre;
	public NodoBase getASTroot(){
		return ast;
	}
:}

/* Simbolos Terminales */

terminal IF;
terminal THEN;
terminal ELSE;
terminal END;
terminal REPEAT;
terminal UNTIL;
terminal READ;
terminal WRITE;
terminal String ID;
terminal Integer NUM;
terminal ASSIGN;
terminal EQ;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal OVER;
terminal LPAREN;
terminal RPAREN;
terminal SEMI;
terminal INT;
terminal VOID;
terminal FOR;
terminal COMA;
terminal OBRACKET;
terminal CBRACKET;
terminal AND;
terminal OR;
terminal NEQUAL;
terminal LT;
terminal GT;
terminal LE;
terminal GE;
terminal RETURN;
terminal BOOLEAN;
terminal BEGIN;
terminal TRUE;
terminal FALSE;

/* Simbolos No Terminales */
non terminal program;
non terminal NodoBase stmt_seq;
non terminal NodoBase stmt;
non terminal NodoBase if_stmt;
non terminal NodoBase repeat_stmt;
non terminal NodoBase assign_stmt;
non terminal NodoBase read_stmt;
non terminal NodoBase write_stmt;
non terminal NodoBase exp;
non terminal NodoBase simple_exp;
non terminal NodoBase term;
non terminal NodoBase factor;
non terminal NodoBase var_stmt;
non terminal NodoBase variable_stmt ;
non terminal NodoBase for_stmt;
non terminal NodoBase funcion_stmt;
non terminal NodoBase exp_var;
non terminal NodoBase encabezado_stmt;
non terminal NodoBase return_stmt;
non terminal NodoBase call_stmt;
non terminal NodoBase par_stmt;
non terminal NodoBase exp_log;

/* Precedencia de operadores */
precedence left OR;
precedence left AND;
precedence left GE,LE,GT,LT,EQ,NEQUAL;
precedence left PLUS, MINUS;
precedence left TIMES, OVER;
precedence left LPAREN,RPAREN,OBRACKET,CBRACKET;

/* REGLAS GRAMATICALES */

program     ::= funcion_stmt:ini BEGIN stmt_seq:sent END 	{: if(modoDepuracion) System.out.println("\t regla program a"); 
						NodoBase hijo = ini;
						if(hijo!=null){
						while(hijo.TieneHermano()){
								hijo = hijo.getHermanoDerecha();
							}
							hijo.setHermanoDerecha(sent);
							ast = ini;
						}
						else{
							ast = sent;
						}
			:}
			| BEGIN stmt_seq:sent END {: if(modoDepuracion) System.out.println("\t regla program b"); 
							ast = sent;
			:}
			;
			
funcion_stmt    ::=  funcion_stmt:ini encabezado_stmt:sent   {: if(modoDepuracion) System.out.println("\t regla funcion a"); 
						NodoBase hijoActual = ini;
						if(hijoActual !=null){
							while(hijoActual.TieneHermano()){
								hijoActual = hijoActual.getHermanoDerecha();
							}
							hijoActual.setHermanoDerecha(sent);
							RESULT=ini;
						}else{
							RESULT=sent;
						}
						:}
            | encabezado_stmt:sent {: if(modoDepuracion) System.out.println("\t regla funcion b"); 
				RESULT=sent;
				:}
		; 
					    
encabezado_stmt ::= VOID ID:nf LPAREN exp_var:p RPAREN BEGIN stmt_seq:cuerpo END {: if(modoDepuracion) System.out.println("\t regla encabezado funcion a"); 
			RESULT = new NodoProcedimiento(nf.toString(),p,cuerpo,tipoFuncion.VOID);
		:}
		|INT ID:nf LPAREN exp_var:p RPAREN BEGIN stmt_seq:cuerpo END {: if(modoDepuracion) System.out.println("\t regla encabezado funcion b"); 
			RESULT = new NodoProcedimiento(nf.toString(),p,cuerpo,tipoFuncion.INT);
		:}
		|BOOLEAN ID:nf LPAREN exp_var:p RPAREN BEGIN stmt_seq:cuerpo END {: if(modoDepuracion) System.out.println("\t regla encabezado funcion c"); 
			RESULT = new NodoProcedimiento(nf.toString(),p,cuerpo,tipoFuncion.BOOLEAN);
		:}
		|  VOID ID:nf LPAREN  RPAREN BEGIN stmt_seq:cuerpo END {: if(modoDepuracion) System.out.println("\t regla encabezado funcion a"); 
			RESULT = new NodoProcedimiento(nf.toString(),cuerpo,tipoFuncion.VOID);
		:}
		|INT ID:nf LPAREN  RPAREN BEGIN stmt_seq:cuerpo END {: if(modoDepuracion) System.out.println("\t regla encabezado funcion b"); 
			RESULT = new NodoProcedimiento(nf.toString(),cuerpo,tipoFuncion.INT);
		:}
		|BOOLEAN ID:nf LPAREN  RPAREN BEGIN stmt_seq:cuerpo END {: if(modoDepuracion) System.out.println("\t regla encabezado funcion c"); 
			RESULT = new NodoProcedimiento(nf.toString(),cuerpo,tipoFuncion.BOOLEAN);
		:}
		;
			  
		  
exp_var ::= INT ID:v COMA exp_var:va {: if(modoDepuracion) System.out.println("\t regla funcion parametros a"); 
			RESULT = new NodoVariable(tipoDato.INT,v.toString(),tipoOp.coma,va);
		  :}
		  |BOOLEAN ID:v COMA exp_var:va {: if(modoDepuracion) System.out.println("\t regla funcion parametros b"); 
			RESULT = new NodoVariable(tipoDato.BOOLEAN,v.toString(),tipoOp.coma,va);
		  :}
		  |INT ID:v {: if(modoDepuracion) System.out.println("\t regla funcion parametros c"); 
			RESULT = new NodoVariable(tipoDato.INT,v.toString());
		  :}
		  | BOOLEAN ID:v {: if(modoDepuracion) System.out.println("\tregla funcion parametros d"); 
			RESULT = new NodoVariable(tipoDato.BOOLEAN,v.toString());
		  :}	
		  ;

stmt_seq    ::= stmt_seq:ini stmt:sent SEMI {: if(modoDepuracion) System.out.println("\t regla stmt_seq stmt_seq"); 
						NodoBase hijoActual = ini;
						if(hijoActual !=null){
							while(hijoActual.TieneHermano()){
								hijoActual = hijoActual.getHermanoDerecha();
							}
							hijoActual.setHermanoDerecha(sent);
							RESULT=ini;
						}else{
							RESULT=sent;
						}
						:}
            | stmt:sent SEMI{: if(modoDepuracion) System.out.println("\t regla stmt_eq stmt"); 
				RESULT=sent;
				:}
		;

stmt     ::= if_stmt:sif	{: if(modoDepuracion) System.out.println("\t regla stmt if_stmt"); 
					RESULT=sif;
					:}
            | repeat_stmt:srep	{: if(modoDepuracion) System.out.println("\t regla stmt repeat_stmt"); 
						RESULT=srep;
						:}
            | assign_stmt:sasi	{: if(modoDepuracion) System.out.println("\t regla stmt assign_stmt"); 
						RESULT=sasi;
						:}
            | read_stmt:slec	{: if(modoDepuracion) System.out.println("\t regla stmt read_stmt"); 
					RESULT=slec;
					:}
            | write_stmt:sesc	{: if(modoDepuracion) System.out.println("\t regla stmt write_stmt"); 
					RESULT=sesc;
					:}
            | variable_stmt:svar	{: if(modoDepuracion) System.out.println("\t regla stmt variable_stmt"); 
					RESULT=svar;
					:}		
			|  for_stmt:sfor	{: if(modoDepuracion) System.out.println("\t regla stmt for_stmt"); 
					RESULT=sfor;
					:}
			| return_stmt:sret	{: if(modoDepuracion) System.out.println("\t regla stmt return_stmt"); 
					RESULT=sret;
					:}    
			| call_stmt:scall	{: if(modoDepuracion) System.out.println("\t regla stmt call_stmt"); 
					RESULT=scall;
					:}
            | error	{: if(modoDepuracion) System.out.println("\t regla stmt error"); 
				System.out.println("Ocurrio error en cup # 0004");
				RESULT=null;
				:}            
            ;

if_stmt     ::= IF exp_log:ex THEN stmt_seq:pthen END {: if(modoDepuracion) System.out.println("\t regla if_stmt if then end "); 
		RESULT = new NodoIf(ex,pthen);
		:}
		| IF exp_log:ex THEN stmt_seq:pthen ELSE stmt_seq:pelse END	{: if(modoDepuracion) System.out.println("\t regla if_stmt if then else end"); 
		RESULT = new NodoIf(ex,pthen,pelse);		
		:}		
		;
		
call_stmt   ::= ID:nombre LPAREN par_stmt:parIzquierdo RPAREN  {: if(modoDepuracion) System.out.println("\t regla call_stmt funcion con parametros"); 
			RESULT=new NodoCall(parIzquierdo,null,nombre.toString());
		:}
		| ID:nombre LPAREN  RPAREN  {: if(modoDepuracion) System.out.println("\t regla call_stmt funcion sin parametros"); 
			RESULT=new NodoCall(null,null,nombre.toString());
		:}
		;

par_stmt ::=  exp_log:ex  COMA par_stmt:v {: if(modoDepuracion) System.out.println("\t regla par_stmt con coma"); 
				RESULT = new NodoCall(ex,v);
		:}
		|  exp_log:ex {: if(modoDepuracion) System.out.println("\t regla par_stmt_log con coma"); 
				RESULT = new NodoCall (ex,null);	
		:}		
		; 

variable_stmt      ::= INT var_stmt:v {: if(modoDepuracion) System.out.println("\t regla variable_stmt int");				
			((NodoVariable)v).setTipo(tipoDato.INT);
			NodoVariable a=(NodoVariable)((NodoVariable)v).getPartev();			
			while(a!=null)
			{
				a.setTipo(tipoDato.INT);
				a=(NodoVariable)a.getPartev();
			}										
			RESULT = v;			
		:}
		| BOOLEAN var_stmt:v {: if(modoDepuracion) System.out.println("\t regla variable_stmt boolean"); 
			((NodoVariable)v).setTipo(tipoDato.BOOLEAN);
			NodoVariable a=(NodoVariable)((NodoVariable)v).getPartev();			
			while(a!=null)
			{
				a.setTipo(tipoDato.BOOLEAN);
				a=(NodoVariable)a.getPartev();
			}
			RESULT = v;
		:}		
		;

var_stmt     ::= ID:variable COMA var_stmt:v {: if(modoDepuracion) System.out.println("\t regla var_stmt id coma"); 
			RESULT = new NodoVariable(variable.toString(),tipoOp.coma,v);
		:}
		| ID:variable {: if(modoDepuracion) System.out.println("\t regla var_stmt id"); 
		RESULT = new NodoVariable(variable.toString(),tipoOp.puntoComa);
		:}	
		;		
		
repeat_stmt ::= REPEAT stmt_seq:cuerpo UNTIL exp:prueba	{: if(modoDepuracion) System.out.println("\t regla repeat_stmt"); 
							RESULT = new NodoRepeat(cuerpo,prueba);
							:}
		;
return_stmt ::= RETURN ID:id {: if(modoDepuracion) System.out.println("\t regla return_stmt id"); 
			RESULT = new NodoReturn(id.toString(),false);
		:}
		| RETURN TRUE {: if(modoDepuracion) System.out.println("\t regla return_stmt true"); 
			RESULT = new NodoReturn("",true);
		:}
		| RETURN FALSE {: if(modoDepuracion) System.out.println("\t regla return_stmt false"); 
			RESULT = new NodoReturn("",false);
		:}
		| RETURN {: if(modoDepuracion) System.out.println("\t regla return_stmt vacio"); 
			RESULT = new NodoReturn();
		:}
		;

assign_stmt ::= ID:variable ASSIGN exp_log:ex	{: if(modoDepuracion) System.out.println("\t regla assign_stmt asignacion normal");
					if(variable!=null)
					RESULT = new NodoAsignacion(variable.toString(),ex);
					else{
					RESULT = null;
					System.out.println("Ocurrio error en cup # 0001");
					}
					:}
		| ID:variable ASSIGN call_stmt:ex {: if(modoDepuracion) System.out.println("\t regla asignacion_stmt llamada de funcion"); 
				if(variable!=null)
				RESULT = new NodoAsignacion(variable.toString(),ex);
			else{
				RESULT = null;
				System.out.println("Ocurrio error en cup # 0001");
				}
				:}
		;

read_stmt   ::= READ ID:variable	{: if(modoDepuracion) System.out.println("\t regla read_stmt read"); 
					System.out.println("Leyendo Variable");
					if(variable!=null)
					RESULT = new NodoLeer(variable.toString());
					else{
					RESULT = null;
					System.out.println("Ocurrio error en cup # 0002");
					}
					:}
		;

write_stmt  ::= WRITE exp:ex	{: if(modoDepuracion) System.out.println("\t regla write_stmt write"); 
					RESULT= new NodoEscribir(ex);
					:}
		;
		
exp_log    ::= exp_log:exI AND exp:exD	{: if(modoDepuracion) System.out.println("\t regla exp AND"); 
							RESULT = new NodoOperacion(exI,tipoOp.and,exD);
							:}
		| exp_log:exI OR exp:exD	{: if(modoDepuracion) System.out.println("\t regla exp OR"); 
							RESULT = new NodoOperacion(exI,tipoOp.or,exD);
							:}
		| exp:exI   {: if(modoDepuracion) System.out.println("\t regla exp"); 
							RESULT = new NodoOperacion(exI,null,null);
							:}
		;

exp         ::= simple_exp:exI GT simple_exp:exD	{: if(modoDepuracion) System.out.println("\t regla exp GT"); 
							RESULT = new NodoOperacion(exI,tipoOp.mayor,exD);
							:}

		| simple_exp:exI LT simple_exp:exD	{: if(modoDepuracion) System.out.println("\t regla exp LT"); 
								RESULT = new NodoOperacion(exI,tipoOp.menor,exD);
								:} 
		
		| simple_exp:exI LE simple_exp:exD	{: if(modoDepuracion) System.out.println("\t regla exp GT"); 
							RESULT = new NodoOperacion(exI,tipoOp.menor_igual,exD);	
							:}
		| simple_exp:exI GE simple_exp:exD	{: if(modoDepuracion) System.out.println("\t regla exp GE"); 
							RESULT = new NodoOperacion(exI,tipoOp.mayor_igual,exD);
					:}						
		| simple_exp:exI EQ simple_exp:exD	{: if(modoDepuracion) System.out.println("\t regla exp EQ"); 
							RESULT = new NodoOperacion(exI,tipoOp.igual,exD);
							:}	
		| simple_exp:exI NEQUAL simple_exp:exD	{: if(modoDepuracion) System.out.println("\t regla exp NEQUAL"); 
							RESULT = new NodoOperacion(exI,tipoOp.diferente,exD);
							:}	
		| simple_exp:ex	{: if(modoDepuracion) System.out.println("\t regla exp"); 
							RESULT = ex;
							:}			
		;
simple_exp  ::= simple_exp:exI PLUS term:exD	{: if(modoDepuracion) System.out.println("\t regla simple_exp PLUS"); 
							RESULT = new NodoOperacion(exI,tipoOp.mas,exD);
							:}
		| simple_exp:exI MINUS term:exD	{: if(modoDepuracion) System.out.println("\t regla simple_exp MINUS"); 
							RESULT = new NodoOperacion(exI,tipoOp.menos,exD);
							:}
		| term:ex	{: if(modoDepuracion) System.out.println("\t regla simple_exp term"); 
				RESULT=ex;
				:}
		;

term        ::= term:exI TIMES factor:exD	{: if(modoDepuracion) System.out.println("\t regla term TIMES"); 
							RESULT = new NodoOperacion(exI,tipoOp.por,exD);
							:}
		| term:exI OVER factor:exD	{: if(modoDepuracion) System.out.println("\t regla term OVER"); 
						RESULT = new NodoOperacion(exI,tipoOp.entre,exD);
						:}
		| factor:ex	{: if(modoDepuracion) System.out.println("\t regla term factor"); 
					RESULT=ex;
					:}
		;

factor      ::= LPAREN exp:ex RPAREN	{: if(modoDepuracion) System.out.println("\t regla factor LPAREN exp RPAREN"); 
							RESULT = ex;
							:}
		| NUM:valor	{: if(modoDepuracion) System.out.println("\t regla factor NUM"); 
					System.out.println("Devolvi nodo numero entero");
					if(valor!=null){
						RESULT= new NodoValor(valor);					
					}else{
						RESULT = null;
						System.out.println("Ocurrio error en cup # 0005");
					}
					:}
		| ID:variable	{: if(modoDepuracion) System.out.println("\t regla factor ID"); 
					if(variable!=null){
						System.out.println("Construyendo Nodo ID para Lexema: "+ variable.toString());
						RESULT= new NodoIdentificador(variable.toString());		
					}else{
						RESULT = null;
						System.out.println("Ocurrio error en cup # 0005");
					}
					:}
		| TRUE	{: if(modoDepuracion) System.out.println("\t regla factor TRUE"); 
					System.out.println("Devolvi nodo true");					
					RESULT= new NodoValor(true);										
					:}
		| FALSE {: if(modoDepuracion) System.out.println("\t regla factor FALSE"); 
					System.out.println("Devolvi nodo false");					
					RESULT= new NodoValor(false);										
					:}
		| error	{: if(modoDepuracion) System.out.println("\t regla factor error"); 
				System.out.println("Ocurrio error en cup # 0003");
				RESULT=null;
				:}
		;
		
for_stmt     ::= FOR LPAREN assign_stmt:asi SEMI exp:ex SEMI assign_stmt:asf RPAREN stmt_seq:cuerpo END {: if(modoDepuracion) System.out.println("\t regla for_stmt"); 
			RESULT = new NodoFor(asi,ex,asf,cuerpo);
		:}
		;